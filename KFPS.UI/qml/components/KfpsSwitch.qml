import QtQuick 6.7
import QtQuick.Controls 6.7
import QtQuick.Layouts 6.7
import Kfps.Theme 1.0

Switch {
    id: root

    objectName: "KfpsSwitch:" + root.text

    property bool dense: false
    property string toolTipText: ""
    readonly property string effectiveToolTipText: toolTipText.trim().length > 0 ? toolTipText : text

    spacing: Theme.px(10)
    leftPadding: 0
    rightPadding: 0
    topPadding: Theme.px(2)
    bottomPadding: Theme.px(2)
    hoverEnabled: true
    focusPolicy: Qt.StrongFocus

    KfpsToolTip {
        visible: root.hovered && root.effectiveToolTipText.length > 0
        text: root.effectiveToolTipText
    }

    implicitHeight: Math.max(
                        Theme.px(dense ? 26 : 30),
                        Math.max(switchTrack.implicitHeight, labelText.implicitHeight) + topPadding + bottomPadding)
    implicitWidth: switchTrack.implicitWidth + spacing + labelText.implicitWidth
    Layout.minimumHeight: root.implicitHeight

    indicator: Rectangle {
        id: switchTrack
        implicitWidth: Theme.px(Theme.classicMode ? (dense ? 16 : 18) : (Theme.terminalMode ? (dense ? 32 : 36) : (dense ? 38 : 42)))
        implicitHeight: Theme.px(Theme.classicMode ? (dense ? 16 : 18) : (dense ? 20 : 22))
        y: Math.round((root.height - height) / 2)
        radius: Theme.corner(height / 2)
        color: Theme.angularControlsEnabled
               ? "transparent"
               : (Theme.classicMode
               ? Theme.checkboxSurface
               : (root.checked
               ? (root.hovered ? Theme.primary : Theme.primaryDeep)
               : (root.hovered ? Theme.checkboxHoverSurface : Theme.switchTrackOff)))
        border.width: Theme.classicMode
                      ? 0
                      : (Theme.angularControlsEnabled
                      ? 0
                      : (root.activeFocus
                      ? Theme.px(2)
                      : (Theme.customFrameExclusive ? 0 : Theme.px(1))))
        border.color: root.activeFocus ? Theme.focusColor
                                       : (root.checked ? (root.hovered ? Theme.focusColor : Theme.primaryBright)
                                                       : (root.hovered ? Theme.primary : Theme.borderSoft))
        scale: Theme.classicMode ? 1.0 : (root.pressed ? 0.96 : (root.hovered ? 1.025 : 1.0))
        Behavior on color { enabled: !Theme.reducedMotion; ColorAnimation { duration: 110 } }
        Behavior on scale { enabled: !Theme.reducedMotion; NumberAnimation { duration: 90; easing.type: Easing.OutCubic } }

        AngularControlFrame {
            anchors.fill: parent
            fillColor: root.checked
                       ? (root.hovered ? Theme.primary : Theme.primaryDeep)
                       : (root.hovered ? Theme.checkboxHoverSurface : Theme.switchTrackOff)
            borderColor: root.activeFocus
                         ? Theme.focusColor
                         : (root.checked ? Theme.signalSecondary : (root.hovered ? Theme.primary : Theme.borderSoft))
            accentColor: Theme.signalSecondary
            hovered: root.hovered
            pressed: root.pressed
            selected: root.checked
            focused: root.activeFocus
            frameEnabled: root.enabled
            cutOverride: Theme.px(5)
            notchOverride: Theme.px(2)
        }

        Rectangle {
            id: switchKnob
            visible: !Theme.terminalMode && !Theme.classicMode
            width: Theme.px(dense ? 14 : 16)
            height: width
            radius: Theme.angularControlsEnabled ? 0 : Theme.corner(width / 2)
            y: Math.round((parent.height - height) / 2)
            x: root.checked ? parent.width - width - Theme.px(3) : Theme.px(3)
            color: Theme.angularControlsEnabled
                   ? "transparent"
                   : (root.hovered ? Theme.primaryHot : (root.checked ? Theme.primaryText : Theme.muted))
            scale: root.pressed ? 0.88 : (root.hovered ? 1.08 : 1.0)

            AngularControlFrame {
                anchors.fill: parent
                fillColor: root.hovered ? Theme.primaryHot : (root.checked ? Theme.primaryText : Theme.muted)
                borderColor: root.checked ? Theme.primaryText : Theme.signalSecondary
                accentColor: Theme.signalSecondary
                hovered: root.hovered
                pressed: root.pressed
                selected: root.checked
                cutOverride: Theme.px(3)
                notchOverride: Theme.px(1.5)
                strokeOverride: Math.max(1, Theme.px(0.8))
            }

            Behavior on x {
                enabled: !Theme.reducedMotion
                NumberAnimation { duration: 150; easing.type: Easing.OutCubic }
            }
            Behavior on color { enabled: !Theme.reducedMotion; ColorAnimation { duration: 100 } }
            Behavior on scale { enabled: !Theme.reducedMotion; NumberAnimation { duration: 90; easing.type: Easing.OutCubic } }
        }

        Text {
            visible: Theme.terminalMode || Theme.classicMode
            anchors.centerIn: parent
            text: Theme.classicMode ? (root.checked ? "X" : "") : (root.checked ? "[X]" : "[ ]")
            color: Theme.classicMode ? Theme.borderStrong : (root.checked ? Theme.primaryText : Theme.text)
            font.family: Theme.monoFamily
            font.pixelSize: Theme.px(dense ? 10 : 11)
            font.weight: Font.Bold
        }

        ClassicBevel {
            anchors.fill: parent
            sunken: true
        }

        ClassicFocusRect {
            anchors.fill: parent
            anchors.margins: -Theme.px(3)
            active: root.activeFocus
        }
    }

    contentItem: Text {
        id: labelText
        leftPadding: switchTrack.implicitWidth + root.spacing
        text: root.text
        font.family: Theme.fontFamily
        font.pixelSize: Theme.px(dense ? 10.5 : 11.5)
        font.capitalization: Theme.terminalMode ? Font.AllUppercase : Font.MixedCase
        color: root.enabled ? Theme.text : Theme.subtle
        opacity: root.hovered ? 1.0 : 0.92
        Behavior on opacity { enabled: !Theme.reducedMotion; NumberAnimation { duration: 100 } }
        verticalAlignment: Text.AlignVCenter
        wrapMode: Text.Wrap
        maximumLineCount: 2
        elide: Text.ElideRight
    }
}
